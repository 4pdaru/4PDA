apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'io.fabric'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.jakewharton.butterknife'
apply plugin: 'com.github.triplet.play'

android {

	compileSdkVersion 25
	buildToolsVersion '25.0.0'

	defaultConfig {
		applicationId 'four.pda'
		versionCode Integer.parseInt(project.versionCode)
		versionName '1.7.3'
		minSdkVersion 16
		targetSdkVersion 25
		buildConfigField 'String', 'VCS_BRANCH', "\"${project.vcsBranch}\""
		buildConfigField 'String', 'VCS_COMMIT', "\"${project.vcsCommit}\""
		multiDexEnabled true
		testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}

	signingConfigs {

		fromProperties {
			storeFile file(project.fourpda_sign_storeFile)
			storePassword project.fourpda_sign_storePassword
			keyAlias project.fourpda_sign_keyAlias
			keyPassword project.fourpda_sign_keyPassword
		}

	}

	buildTypes {

		debug {
			applicationIdSuffix '.debug'
			signingConfig signingConfigs.fromProperties
		}

		release {
			signingConfig signingConfigs.fromProperties
			minifyEnabled true
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}

	}

	productFlavors {

		dummy {
			applicationIdSuffix '.dummy'
		}

		production {
		}

	}

	lintOptions {
		//TODO Заигнорить нормально okhttp
		warning 'InvalidPackage'
	}

	applicationVariants.all { variant ->
		variant.outputs.each { output ->
			def name = output.outputFile.name
					.replace('app-', 'FourPDA-')
					.replace('.apk', "-${variant.versionName}-b${variant.versionCode}.apk")
					.replace('-release', '')
			output.outputFile = new File(output.outputFile.parent, name)
		}
	}

}

play {
	jsonFile = file(project.fourpda_playPublisher_jsonFile)
}

configurations {
	apt
}

apt {
	arguments {
		logLevel 'warn'
		logAppenderConsole true
		androidManifestFile 'app/src/main/AndroidManifest.xml'
	}
}

def supportVersion = '25.0.1'

dependencies {

	compile project(':dao')
	compile project(':client')
	compile project(':html_templates')

	apt 'org.androidannotations:androidannotations:4.1.0'
	compile 'org.androidannotations:androidannotations-api:4.1.0'

	apt 'com.google.dagger:dagger-compiler:2.7'
	compile 'com.google.dagger:dagger:2.7'
	compile 'javax.annotation:javax.annotation-api:1.2'

	compile "com.android.support:appcompat-v7:$supportVersion"
	compile "com.android.support:recyclerview-v7:$supportVersion"
	compile "com.android.support:design:$supportVersion"
	compile "com.android.support:support-annotations:$supportVersion"

    compile 'com.squareup.okhttp3:okhttp:3.4.2'
	compile 'com.squareup.picasso:picasso:2.5.2'

	compile 'com.github.swapii:PersistentCookieJar:1.0.0'

	compile 'com.jakewharton:butterknife:8.4.0'
	apt 'com.jakewharton:butterknife-compiler:8.4.0'

	compile 'de.greenrobot:eventbus:2.4.0'

	provided 'frankiesardo:auto-parcel:1.0.3'
	apt 'frankiesardo:auto-parcel:1.0.3'

    compile('com.crashlytics.sdk.android:crashlytics:2.6.5@aar') {
        transitive = true;
    }

	compile 'org.slf4j:slf4j-api:1.7.21'

	compile 'com.github.rey5137:material:1.2.4'
	compile 'net.opacapp:multiline-collapsingtoolbar:1.3.1'

	compile 'com.github.chrisbanes:PhotoView:1.3.1'

	androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
	androidTestCompile 'com.android.support.test:runner:0.5'
	androidTestCompile 'com.android.support.test:rules:0.5'
	androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'
	androidTestCompile ('com.android.support.test.espresso:espresso-core:2.2.1') {
		exclude group: 'com.squareup.dagger'
	}
	androidTestCompile 'junit:junit:4.12'

	testCompile 'junit:junit:4.12'

}

configurations.all {
	resolutionStrategy.force "com.android.support:support-annotations:$supportVersion"
}
