apply plugin: 'com.android.application'
apply plugin: 'android-apt'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'io.fabric'

android {

	compileSdkVersion 23
	buildToolsVersion '23.0.1'

	defaultConfig {
		applicationId 'four.pda'
		versionCode Integer.parseInt(project.versionCode)
		versionName '1.1.0'
		minSdkVersion 16
		targetSdkVersion 23
	}

	signingConfigs {

		develop {
			storeFile file('develop.keystore')
			storePassword 'develop'
			keyAlias 'fourpda_develop'
			keyPassword 'develop'
		}

	}

	buildTypes {

		debug {
			applicationIdSuffix ".debug"
			signingConfig signingConfigs.develop
		}

		release {
			signingConfig signingConfigs.develop
		}

	}

	lintOptions {
		//TODO Заигнорить нормально okhttp
		warning 'InvalidPackage'
	}

	applicationVariants.all { variant ->
		variant.outputs.each { output ->
			def name = output.outputFile.name
					.replace('app-', 'FourPDA-')
					.replace('.apk', "-${variant.versionName}-b${variant.versionCode}.apk")
					.replace('-release', '')
			output.outputFile = new File(output.outputFile.parent, name)
		}
	}

}

configurations {
	apt
}

apt {
	arguments {
		logLevel 'warn'
		logAppenderConsole true
		androidManifestFile 'app/src/main/AndroidManifest.xml'
	}
}

dependencies {

	compile project(':dao')
	compile project(':client')

	apt 'org.androidannotations:androidannotations:3.3.2'
	compile 'org.androidannotations:androidannotations-api:3.3.2'

	def supportVersion = '23.0.1'

	compile "com.android.support:appcompat-v7:$supportVersion"
	compile "com.android.support:recyclerview-v7:$supportVersion"
	compile "com.android.support:design:$supportVersion"

    compile 'com.google.android.gms:play-services-analytics:8.1.0'

	compile 'com.squareup.okhttp:okhttp:2.3.0'
	compile 'com.squareup.picasso:picasso:2.5.0'

	compile 'com.jakewharton:butterknife:7.0.1'

	compile 'de.greenrobot:eventbus:2.4.0'

    compile('com.crashlytics.sdk.android:crashlytics:2.5.2@aar') {
        transitive = true;
    }

	compile 'org.slf4j:slf4j-api:1.7.12'

}
