apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.github.triplet.play'

android {

	compileSdkVersion 25

	defaultConfig {
		applicationId 'four.pda'
		versionCode Integer.parseInt(project.versionCode)
		versionName '1.7.9'
		minSdkVersion 16
		targetSdkVersion 25
		buildConfigField 'String', 'VCS_BRANCH', "\"${project.vcsBranch}\""
		buildConfigField 'String', 'VCS_COMMIT', "\"${project.vcsCommit}\""
		multiDexEnabled true
		testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'

		javaCompileOptions {
			annotationProcessorOptions {
				arguments = ["resourcePackageName": android.defaultConfig.applicationId]
			}
		}

		setProperty('archivesBaseName', "FourPDA-$versionName.$versionCode")

	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}

	signingConfigs {

		fromProperties {
			storeFile file(project.fourpda_sign_storeFile)
			storePassword project.fourpda_sign_storePassword
			keyAlias project.fourpda_sign_keyAlias
			keyPassword project.fourpda_sign_keyPassword
		}

	}

	buildTypes {

		debug {
			applicationIdSuffix '.debug'
			signingConfig signingConfigs.fromProperties
		}

		release {
			signingConfig signingConfigs.fromProperties
			minifyEnabled true
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}

	}

	flavorDimensions 'type'

	productFlavors {

		dummy {
			dimension 'type'
			applicationIdSuffix '.dummy'
		}

		production {
			dimension 'type'
		}

	}

	lintOptions {
		//TODO Заигнорить нормально okhttp
		warning 'InvalidPackage'
	}

}

play {
	jsonFile = file(project.fourpda_playPublisher_jsonFile)
}

def supportVersion = '25.0.1'

dependencies {

	implementation project(':dao')
	implementation project(':client')
	implementation project(':html_templates')

	annotationProcessor 'org.androidannotations:androidannotations:4.4.0'
	implementation 'org.androidannotations:androidannotations-api:4.4.0'

	annotationProcessor 'com.google.dagger:dagger-compiler:2.7'
	implementation 'com.google.dagger:dagger:2.7'
	implementation 'javax.annotation:javax.annotation-api:1.2'

	implementation 'org.apache.commons:commons-lang3:3.5'

	/**
	 * Support v4 library need for Glide
	 * https://github.com/bumptech/glide#download
	 *
	 * TODO Проверить дерево зависимостей, есть ли там уже support-v4
	 */
	implementation "com.android.support:support-v4:$supportVersion"
	implementation "com.android.support:appcompat-v7:$supportVersion"
	implementation "com.android.support:recyclerview-v7:$supportVersion"
	implementation "com.android.support:design:$supportVersion"
	implementation "com.android.support:support-annotations:$supportVersion"

	implementation 'com.squareup.okhttp3:okhttp:3.9.1'
	implementation 'com.squareup.picasso:picasso:2.5.2'

	implementation 'com.github.bumptech.glide:glide:3.7.0'

	implementation 'com.github.swapii:PersistentCookieJar:1.0.0'

	implementation 'com.jakewharton:butterknife:8.4.0'
	annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'

	implementation 'de.greenrobot:eventbus:2.4.0'

	compileOnly 'frankiesardo:auto-parcel:1.0.3'
	annotationProcessor 'frankiesardo:auto-parcel:1.0.3'

	implementation('com.crashlytics.sdk.android:crashlytics:2.6.5')

	implementation 'org.slf4j:slf4j-api:1.7.25'

	implementation 'com.github.rey5137:material:1.2.4'
	implementation 'net.opacapp:multiline-collapsingtoolbar:1.3.1'

	implementation 'com.github.chrisbanes:PhotoView:1.3.1'

	androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
	androidTestImplementation 'com.android.support.test:runner:0.5'
	androidTestImplementation 'com.android.support.test:rules:0.5'
	androidTestImplementation 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'
	androidTestImplementation ('com.android.support.test.espresso:espresso-core:2.2.1') {
		exclude group: 'com.squareup.dagger'
	}
	androidTestImplementation 'junit:junit:4.12'

	testImplementation 'junit:junit:4.12'

}

configurations.all {
	resolutionStrategy.force "com.android.support:support-annotations:$supportVersion"
}
